"""Runs ``plink`` QC steps using different call rate thresholds.

Outputs statistics for unfiltered, call rate 1, and call rate 2. Statistics
include sample/variant rates of missing data, sex consistency using X-linked
variants, minor allele frequency, and Hardy-Weinberg Equilibrium.
"""

rule plink_sample_qc_stats:
    """Runs ``plink`` statistics.

    ``plink`` will calculate a number of statistics by passing them as a flag.

    .. rubric:: Statistics

        :imiss`: sample-based missing data report
        :lmiss`: variant-based missing data report
        :sexcheck`: Runs X chromosome based sanity checks to verify sex
        :frq`: The minor allele frequency (MAF)
        :hwe`: Checks variants are in HWE

    .. resources::
        :memory: 10GB

    """
    input:
        "{d}/samples.bed",
        "{d}/samples.bim",
        "{d}/samples.fam",
    params:
        inProj="{d}/samples",
        outProj="{d}/samples{filt}",
    output:
        "{d}/samples{filt}.imiss",
        "{d}/samples{filt}.lmiss",
        "{d}/samples{filt}.sexcheck",
        "{d}/samples{filt}.frq",
        "{d}/samples{filt}.hwe",
    shell:
        "plink --bfile {params.inProj} --freq --missing --hardy --het --check-sex --memory 10000 --out {params.outProj}"


rule filter_call_rate_1:
    """Outputs ``plink`` dataset removing samples and variants below call rate filter 1.

    Call rate filter 1 is usually (1 - 0.8) missing for samples and variants.

    .. resources::
        :memory: 10GB

    """
    input:
        "plink_start/samples.bed",
        "plink_start/samples.bim",
        "plink_start/samples.fam",
    params:
        inProj="plink_start/samples",
        outProj="plink_filter_call_rate_1/samples",
        mind=str(1 - float(samp_cr_1)),
        geno=str(1 - float(snp_cr_1)),
    output:
        "plink_filter_call_rate_1/samples.bed",
        "plink_filter_call_rate_1/samples.bim",
        "plink_filter_call_rate_1/samples.fam",
    shell:
        "plink --bfile {params.inProj} --mind {params.mind} --geno {params.geno} --make-bed --memory 10000 --out {params.outProj}"


rule filter_call_rate_2:
    """Outputs ``plink`` dataset removing samples and variants below call rate filter 2.

    Call rate filter 2 is usually (1 - 0.95) missing for samples and variants.

    .. resources::
        :memory: 10GB

    """
    input:
        "plink_filter_call_rate_1/samples.bed",
        "plink_filter_call_rate_1/samples.bim",
        "plink_filter_call_rate_1/samples.fam",
    params:
        inProj="plink_filter_call_rate_1/samples",
        outProj="plink_filter_call_rate_2/samples",
        mind=str(1 - float(samp_cr_2)),
        geno=str(1 - float(snp_cr_2)),
    output:
        "plink_filter_call_rate_2/samples.bed",
        "plink_filter_call_rate_2/samples.bim",
        "plink_filter_call_rate_2/samples.fam",
    shell:
        "plink --bfile {params.inProj} --mind {params.mind} --geno {params.geno} --memory 10000 --make-bed --out {params.outProj}"
